plugins {
  id "com.moowork.node" version "0.13"
}

node {
  version = '6.9.1'
  npmVersion = '3.10.9'
  download = true
  // Set the work directory for unpacking node
  workDir = file("${project.buildDir}/nodejs")
  // Set the work directory where node_modules should be located
  nodeModulesDir = file("${project.projectDir}")
}

task npmCacheConfig(type: NpmTask) {
    description = "Configure the NPM cache"
    def npmCacheDir = "${gradle.getGradleUserHomeDir()}/caches/npm"
    outputs.files file(npmCacheDir)
    args = [ 'config', 'set', 'cache', npmCacheDir ]
}

task bowerInstall(type: NodeTask, dependsOn: 'npm_install') {
    script = file('node_modules/bower/bin/bower')
    def bowerComponentsDir = "app/bower_components"
    args = ["--config.directory=${bowerComponentsDir}",
            "--config.storage.cache=${gradle.getGradleUserHomeDir()}/caches/bower/cache",
            "--config.storage.packages=${gradle.getGradleUserHomeDir()}/caches/bower/packages",
            "--config.storage.registry=${gradle.getGradleUserHomeDir()}/caches/bower/registry",
            "install"]
    workingDir = file("${project.projectDir}")
    inputs.files file('bower.json')
    outputs.files file("${bowerComponentsDir}")
}

task gulpDist(type: NodeTask, dependsOn: 'bowerInstall') {
    script = file('node_modules/gulp/bin/gulp.js')
    args = ["build"]
    workingDir = file("${project.projectDir}")
    inputs.files fileTree('app')
    outputs.files fileTree('dist')
}

task clean << {
    delete(file('app/bower_components'))
    delete(file('dist'))
}

task gulpServe(type: NodeTask, dependsOn: 'bowerInstall') {
    script = file('node_modules/gulp/bin/gulp.js')
    args = ["serve"]
    workingDir = file("${project.projectDir}")
}

task gulpServeLocal(type: NodeTask, dependsOn: 'bowerInstall') {
    script = file('node_modules/gulp/bin/gulp.js')
    args = ["serveLocal"]
    workingDir = file("${project.projectDir}")
}
